<!DOCTYPE html>
<!--
WinSetView (Globally Set Explorer Folder Views)
Les Ferch, lesferch@gmail.com, GitHub repository created 2021-03-26
WinSetView.hta (GUI tool to select desired views)
-->
<html>
<head>
<title>WinSetView</title>
<meta charset="UTF-8" http-equiv="X-UA-Compatible" content="IE=8">
<hta:application
  id=oHTA
  icon=AppParts\WinSetView.ico
  applicationname=WinSetView
  selection=yes
  singleinstance=yes
  contextmenu=no
>
<script language="VBScript">
Option Explicit

Const Version = "2.65"

SetLocale(1033)
Const ForReading = 1
Const ForWriting = 2
Const Unicode = -1
Const Ansi = 0

Dim o, oWSH, oFSO, oFolder, oFile, oFiles, oOption, oElement, oElements
Dim oFolderTypes, oSettings, oSearchOnly, oOption1, oOption2, oThemes
Dim i, j, Q, V, X, Y, Z, Temp, TestFile, Section, CheckCount, Value, Key, ArrKeys, Result, SubKey
Dim AvailHeight, HeadHeight, RequiredWidth, CurrentWidth, Maximized, Fonts, TestFileName
Dim AppData, NTVer, WinVer, MajVer, Folder, INIFile, rbShow, BodyHTML, TaggedHTML, Tag, Item
Dim Language,LangDir, LangFile, LangCount, LangData, LangIndex, UBFolderType, YScrollPos
Dim ViewFile, ViewData, FolderTypeFile, Scale, FontsFile, Backup, F1, F2, FTHide, KeepAdvanced
Dim ColFile, ColData, SelectedFont1, SelectedFont2, FontFile, LRArrow, ResetThumbs
Dim CmdLine, Line, Order, sOrder, SelectedSize1, SelectedSize2, GUID, Found, VirtualStore
Dim C1, C2, LastColSel, FTindex, Data, Show, TempFile, File, PartsDir, FileDialogExe
Dim ColWid, ColShow, SortBy, BaseScale, UpArrow, DnArrow, MyPath, MyFolder, a, b, c
Dim Gidx, Midx, Nidx, Pidx, Widx, S1idx, S2idx, S3idx, S4idx, Sort1, Sort2, Sort3, Sort4
Dim ArrSort1, ArrSort2, ArrSort3, ArrSort4, ArrFonts
Dim ArrSort1Order, ArrSort2Order, ArrSort3Order, ArrSort4Order, ArrGroupByOrder
Dim ArrFD, ArrFDV, ArrFDG, LParen, RParen, DetailsIcon, RightClickIcon
Dim ArrLang, ArrView, ArrColData, ArrColDataItem, ArrColumnEntry, ArrColumnList, ArrLine
Dim ArrData(21), ArrFolderTypeGUID(60), ArrFolderType(60), ArrFolderTypeLang(60), ArrInherit(60)
Dim ArrColumnLists, ArrGroupBy, ArrInheritFlag, ArrHTML1, ArrHTML2, ArrViewChoice
Dim ArrSortBy, ArrIncludeFlag, ArrIconSize, ArrThemes, ModalHTA, FileDialogHTA
Dim ColShowLabels, FTCopyIdx, ThemeFile, SectionList, sTheme, CurBld, LWinVer
Dim TempArray, RetVal, DialogArgs, DialogOptions, id, GrpUpArrow, GrpDnArrow
Dim Warning, OptIcon, DialogTop, HighContrast, Beta
Dim Txt1Data, Txt2Data, WinSys, ExeErr, ArrExe
Dim WinSizePos, aWinSizePos, GotError

Z = vbCRLF
Q = Chr(34)
Language = "en-US"
KeepAdvanced = False
Beta = Instr(LCase(Version),"beta")>0

Set oFSO = CreateObject("Scripting.FileSystemObject")
Set oWSH = CreateObject("Wscript.Shell")

WinSizePos = ""
On Error Resume Next
WinSizePos = oWSH.RegRead("HKCU\Software\WinSetView\WinSizePos")
On Error Goto 0

If WinSizePos<>"" Then
  aWinSizePos = Split(WinSizePos,",")
  On Error Resume Next
  Window.ResizeTo aWinSizePos(0),aWinSizePos(1)
  Window.MoveTo aWinSizePos(2),aWinSizePos(3)
  GotError = Err.Number<>0
  On Error Goto 0
End If

If WinSizePos="" Or GotError Then
  RequiredWidth = 700
  If RequiredWidth>screen.availWidth Then RequiredWidth = screen.availWidth
  Window.ResizeTo RequiredWidth, screen.availHeight
  Window.MoveTo (screen.availWidth - RequiredWidth) / 2, 4
End If

Set oSearchOnly = CreateObject("Scripting.Dictionary")
oSearchOnly.Add "ItemFolderPathDisplay",""
oSearchOnly.Add "ItemFolderPathDisplayNarrow",""
oSearchOnly.Add "ItemPathDisplay",""
oSearchOnly.Add "ItemFolderNameDisplay",""

BaseScale = GetScale(): Scale = BaseScale

Main

Sub Main

' Determine Windows version (also works for Windows Server)
' Treat Windows 11 same as Windows 10 because they have same folder types and properties

NTVer = oWSH.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\CurrentVersion")
If NTVer="6.1" Then WinVer = "7"
If NTVer="6.2" Or NTVer="6.3" Then WinVer = "8"
On Error Resume Next
MajVer = oWSH.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\CurrentMajorVersionNumber")
CurBld = oWSH.RegRead("HKLM\Software\Microsoft\Windows NT\CurrentVersion\CurrentBuild")
On Error Goto 0
If MajVer>=10 Then WinVer = "10"
LWinVer = WinVer
If CurBld>=22000 Then LWinVer = "11"

If WinVer<>"7" And WinVer<>"8" And WinVer<>"10" Then
  MsgBox "Windows 7, 8, 10, or higher is required.",vbCritical,"Error"
  Self.Close
  Exit Sub
End If

HighContrast = oWSH.RegRead("HKCU\Control Panel\Accessibility\HighContrast\Flags")="127"

' This code allows the script to run from a UNC or mapped drive
' whether it's double-clicked or found with Windows Search

MyPath = Mid(document.URL,8)
MyFolder = oFSO.GetParentFolderName(MyPath)
oWSH.CurrentDirectory = MyFolder

If InStr(MyFolder,"[") Or InStr(MyFolder,"]") Then
  a = "Current directory: " & MyFolder & Z & Z
  b = "Due to a PowerShell issue, this script cannot run from a directory containing '[' or ']'"
  c = Z & Z & "Please rename the current directory."
  MsgBox a & b & c,vbCritical,"Error"
  Self.Close
  Exit Sub
End If
  
Temp = oWSH.ExpandEnvironmentStrings("%Temp%")
TempFile = Temp & "\WinSetView.tmp"

LangDir = MyFolder & "\Language\"
PartsDir = MyFolder & "\AppParts\"
AppData = MyFolder & "\AppData"
Backup = MyFolder & "\AppData\Backup"
FontsFile = PartsDir & "Fonts.txt"
FileDialogExe = PartsDir & "FileDialog.exe"
ModalHTA = PartsDir & "Modal.hta"
FileDialogHTA = PartsDir & "FileDialog.hta"
ThemeFile = PartsDir & "Themes.ini"

If WinVer="7" Or WinVer="8" Then
  UpArrow = "↑"
  DnArrow = "↓"
  GrpUpArrow = "(↑)"
  GrpDnArrow = "(↓)"
  LRArrow = "←→"
  OptIcon = "☼"
  Warning = "!"
  DetailsIcon = "⋮⋮"
  RightClickIcon = "⋮"
Else
  UpArrow = "⮬"
  DnArrow = "⮯"
  GrpUpArrow = "(⮬)"
  GrpDnArrow = "(⮯)"
  LRArrow = "⟷"
  OptIcon = "⚙"
  Warning = "⚠"
  DetailsIcon = "⋮⋮⋮"
  RightClickIcon = "⋮"
End If

' Set AppData to current folder if we have write access
' Otherwise set AppData to user profile AppData

TestFileName = Replace(Replace(Replace(Now(),"/",""),":","")," ","") & ".txt"
TestFile = ".\" & TestFileName
On Error Resume Next
oFSO.OpenTextFile(TestFile,ForWriting,True,Ansi).Write("")
On Error Goto 0
If oFSO.FileExists(TestFile) Then
  VirtualStore = oWSH.ExpandEnvironmentStrings("%LOCALAPPDATA%") & "\VirtualStore" & Mid(MyFolder,3) & "\"
  If oFSO.FileExists(VirtualStore & TestFileName) Then AppData = VirtualStore & "AppData"
  oFSO.DeleteFile(TestFile)
Else
  AppData = oWSH.ExpandEnvironmentStrings("%APPDATA%") & "\WinSetView"
  Backup = AppData & "\Backup"
End If
If Not oFSO.FolderExists(AppData) Then oFSO.CreateFolder(AppData)
If Not oFSO.FolderExists(Backup) Then oFSO.CreateFolder(Backup)

' Copy INI files to redirected AppData folder

If AppData <> MyFolder & "\AppData" Then
  On Error Resume Next
  oFSO.CopyFile MyFolder & "\AppData\*.ini",AppData & "\", False
  On Error Goto 0
End If

' See if there are any backup reg files in the Backup folder

Set oFolder = oFSO.GetFolder(Backup)
Set oFiles = oFolder.Files
rbShow = False
For Each oFile In oFiles
  If LCase(oFSO.GetExtensionName(oFile.Name)) = "reg" Then rbShow = True
Next

' Get data from INI file

INIFile = AppData & "\Win" & WinVer & ".ini"
Set oSettings = CreateObject("Scripting.Dictionary")
Set oThemes = CreateObject("Scripting.Dictionary")
If oFSO.FileExists(INIFile) Then Set oSettings = Ini2Dict(INIFile)
If oFSO.FileExists(ThemeFile) Then Set oThemes = Ini2Dict(ThemeFile)
ArrThemes = Split(SectionList,"|")

' Set language choice from INI or Windows
SetLang

End Sub

'Calculate current window size and position and save to registry

Sub window_OnBeforeUnload
  Dim w, h, xpad, ypad, wpad, hpad
  Document.documentElement.Style.overflow = "hidden"
  w = Document.documentElement.clientWidth
  h = Document.documentElement.clientHeight
  x = window.screenLeft
  y = window.screenTop
  window.MoveTo screen.Width,screen.Height
  window.ResizeTo 700,700
  wpad = 700 - Document.documentElement.clientWidth
  hpad = 700 - Document.documentElement.clientHeight
  xpad = window.screenLeft - screen.Width
  ypad = window.screenTop - screen.Height
  oWSH.RegWrite "HKCU\Software\WinSetView\WinSizePos",w+wpad & "," & h+hpad & "," & x-xpad & "," & y-ypad
End Sub

' On exit kill any open instances of filedialog.exe

Sub window_OnUnload
  oWSH.Run "TaskKill /im FileDialog.exe /f",0,False
End Sub

Sub window_OnLoad

  Vp1.innerHTML = Version
  Vp2.innerHTML = Version
  Vp3.innerHTML = Version

  'Populate language dropdown menu
  
  LangCount = 0
  If oFSO.FolderExists(LangDir) Then
    Set oFolder = oFSO.GetFolder(LangDir)
    For Each oFolder In oFolder.SubFolders
      Folder = oFSO.GetBaseName(oFolder)
      LangCount = LangCount + 1
      Set oOption = Document.createElement("Option")
      oOption.Text = Folder 
      oOption.Value = Folder 
      Lang.Add(oOption) 
    Next
  End If

  ' Set language drop down item to selected language

  If LangCount>0 Then
    LangIndex = 0
    For i = 0 To (LangCount - 1)
      If LCase(Language)=LCase(lang.options(i).value) Then
        LangIndex = i
        Exit For
      End If
    Next
    lang.SelectedIndex = LangIndex
  End If

  ' Populate font menu

  For i = 8 to 24
    Set oOption1 = Document.createElement("Option")
    oOption1.Text = i
    oOption1.Value = i
    Size1.Add(oOption1)
    Set oOption2 = Document.createElement("Option")
    oOption2.Text = i
    oOption2.Value = i
    Size2.Add(oOption2)
  Next

  ' Populate horizontal scroll control menu

  For i = 0 to 9
    Set oOption = Document.createElement("Option")
    If i=0 Then
      oOption.Disabled = True
      oOption.Text =  "⟷"
      oOption.Value = i
    Else
      oOption.Text = i
      oOption.Value = i
    End If
    xs.Add(oOption)
  Next
  
  cbo.Value = DetailsIcon
  cbc.Value = RightClickIcon

  If WinVer="7" Or WinVer="8" Then
    bc1.Value = "⊕"
    bc2.Value = "⊕"
    bc3.Value = "⊕"
    ba.Value = "↑↓"
  End If

  ' Save current body HTML so that language can be switched without restarting
  TaggedHTML = Document.Body.innerHTML
  
  ' Set up pages in selected language and font
  UpdateLang False

  ExeCheck

End Sub

Sub ExeCheck
  oWSH.Run "TaskKill /im FileDialog.exe /f",0,False
  WinSys = oWSH.ExpandEnvironmentStrings("%SystemRoot%") & "\System32\"
  If Not oFSO.FileExists(WinSys & "WindowsPowerShell\v1.0\PowerShell.exe") Then ExeErr = ExeErr & "PowerShell.exe" & Z
  ArrExe = Split("TaskKill.exe,Reg.exe,Cmd.exe",",")
  For i = 0 To UBound(ArrExe)
    If Not oFSO.FileExists(WinSys & ArrExe(i)) Then ExeErr = ExeErr & ArrExe(i) & Z
  Next
  If ExeErr<>"" Then
    MsgBox "The following required system files were not found:" & Z & Z & ExeErr,vbCritical,"Error"
    Self.Close
    Exit Sub
  End If
End Sub

Sub UpdateCheck
  Dim oReq, BetaVer, FullVer, CheckVer, MyVer
  Set oReq = CreateObject("Msxml2.XMLHttp")
  x = "https://raw.githubusercontent.com/LesFerch/WinSetView/main/VersionHistory.md"
  If Beta Then x = "https://raw.githubusercontent.com/LesFerch/WinSetView/beta/VersionHistory.md"
  On Error Resume Next
  oReq.Open "GET", x, False
  oReq.Send
  x = oReq.ResponseText
  On Error Goto 0
  FullVer = Mid(x,4,9)
  BetaVer = InStr(LCase(FullVer),"beta")>0
  CheckVer = Left(FullVer,4)
  MyVer = Left(Version,4)
  If Not IsNumeric(CheckVer) Then Exit Sub
  If CheckVer>MyVer Or (Beta And (CheckVer=MyVer) and (Not BetaVer)) Then
    If Not BetaVer Then Beta = False
    hb.Value = ArrLang(46)
    Vp1.innerHTML = Version & " (" & ArrLang(45) & " " & CheckVer & ")"
    UpdateRequiredWidth
  End If
End Sub

Sub UpdateRequiredWidth
  RequiredWidth = twid.ScrollWidth + Int(BaseScale * 50)
  If RequiredWidth>screen.availWidth Then RequiredWidth = screen.availWidth
End Sub

' The following code is needed for scaling the checkboxes and radio buttons

Function GetScale()
  GetScale = 1.0
  On Error Resume Next
  'If user changes scale, they must logout/login for this registry value to change
  GetScale = oWSH.RegRead("HKCU\Control Panel\Desktop\WindowMetrics\AppliedDPI") / 96
  On Error Goto 0
End Function

' Get data from INI file into scripting dictionary

Function Ini2Dict(INIFile)
  SectionList = ""
  Dim oDict
  Set oDict = CreateObject("Scripting.Dictionary")
  If oFSO.FileExists(IniFile) Then 
    Set oFile = oFSO.OpenTextFile(IniFile,ForReading,,Ansi)
    Do Until oFile.AtEndOfStream
      Line = Trim(oFile.ReadLine)
      If Line<>"" And Left(Line,1)<>";" Then
        If Left(Line,1)="[" Then
          Section = Line
          If SectionList<>"" Then SectionList = SectionList & "|"
          SectionList = SectionList & Replace(Replace(Section,"[",""),"]","")
        Else
          ArrLine = Split(Line,"=")
          If UBound(Arrline)=1 Then oDict.Add Section & ArrLine(0),ArrLine(1)
        End If
      End If
    Loop
    oFile.Close
    Set Ini2Dict = oDict
  End If
End Function

' Set preferred language

Sub SetLang
  Language = "en-US"
  On Error Resume Next
  Language = oWSH.RegRead("HKCU\Control Panel\International\LocaleName")
  Language = oWSH.RegRead("HKCU\Control Panel\Desktop\PreferredUILanguages")(0)
  On Error Goto 0
  If oSettings.Exists("[Options]Language") Then Language = oSettings.Item("[Options]Language")
  If Not oFSO.FolderExists(LangDir & Language) Then Language = "en-US"
  If Not oFSO.FolderExists(LangDir & Language) Then
    If InStr(MyFolder,Temp) Then
      MsgBox "Cannot run directly from zip file." & Z & Z & "Please EXTRACT entire zip file and then run.",vbCritical,"Error"
    Else
      MsgBox "Language folder missing: " & LangDir & Language,vbCritical,"Error"
    End If
    Self.Close
    Exit Sub
  End If
End Sub

' Update pages with selected language, font, and size

Sub FTHideUnHide(i,HideAll)
  If ArrIncludeFlag(i)="0" Or HideAll Then
    Document.GetElementByID(i & "FTtitle").ClassName = "ftl"
    Document.GetElementByID(i & "FTsettings").Style.Display = "none"
    Document.GetElementByID(i & "Include").Checked = False
    Document.GetElementByID(i & "Inspect").Style.Display = "none"
  Else
    Document.GetElementByID(i & "FTtitle").ClassName = "ft"
    Document.GetElementByID(i & "FTsettings").Style.Display = "block"
    Document.GetElementByID(i & "Include").Checked = True
    If IFace.SelectedIndex=2 Then Document.GetElementByID(i & "Inspect").Style.Display = "inline"
  End If
End Sub

Sub UpdateLang(OnChange)

  'Read in language file
  If LangCount>0 Then
    LangIndex = lang.SelectedIndex
    Language = lang.options(LangIndex).value
    LangFile = LangDir & Language & "\Labels.txt"
    If oFSO.FileExists(LangFile) Then
      LangData = oFSO.OpenTextFile(LangFile,ForReading,,Unicode).ReadAll
    End If
  End If

  ' Make fist array element a blank line so that text editor line numbers match array indices
  ArrLang = Split(Z & LangData,Z)

  FolderTypeFile = LangDir & Language & "\FolderTypes.txt"

  ' Read FolderType file contents into an array
  ' If Win 7 or 8, skip folder types that don't exist

  If oFSO.FileExists(FolderTypeFile) Then
    Set oFile = oFSO.OpenTextFile(FolderTypeFile,ForReading,,Unicode)
    Set oFolderTypes = CreateObject("Scripting.Dictionary")
    i = -1
    Do Until oFile.AtEndOfStream
      Line = Trim(oFile.ReadLine)
      If Line<>"" And Left(Line,1)<>";" And InStr(Line,";") Then
        ArrLine = Split(Line,";")
        GUID = Trim(ArrLine(0))
        Found = True
        If Not(WinVer="10" Or i=-1) Then
          On Error Resume Next
          oWSH.RegRead "HKLM\Software\Microsoft\Windows\CurrentVersion\Explorer\FolderTypes\" & GUID & "\CanonicalName"
          If Err.Number<>0 Then Found = False
          On Error Goto 0
          If InStr(ArrLine(1),"StorageProvider") Then Found = False
        End If
        If Found Then
          i = i + 1
          ArrFolderTypeGUID(i) = GUID
          ArrFolderType(i) = Trim(ArrLine(1))
          ArrFolderTypeLang(i) = Trim(ArrLine(2))
          oFolderTypes.Add ArrLine(1),i
        End If
      End If
    Loop
    oFile.Close
  Else
    ArrFolderType(0) = "Global"
    ArrFolderTypeLang(0) = "Global"
  End If
  
  ' Set upper bound of folder types
  UBFolderType = i

  ReDim ArrSort1(UBFolderType), ArrSort2(UBFolderType), ArrSort3(UBFolderType), ArrSort4(UBFolderType)
  ReDim ArrSort1Order(UBFolderType), ArrSort2Order(UBFolderType), ArrSort3Order(UBFolderType), ArrSort4Order(UBFolderType)
  ReDim ArrColumnLists(UBFolderType), ArrGroupBy(UBFolderType), ArrViewChoice(UBFolderType), ArrIconSize(UBFolderType)
  ReDim ArrHTML1(UBFolderType), ArrInheritFlag(UBFolderType), ArrIncludeFlag(UBFolderType), ArrGroupByOrder(UBFolderType)
  ReDim ArrFD(UBFolderType), ArrFDV(UBFolderType), ArrFDG(UBFolderType)
  
  ' Set up inheritance based upon first string match up to first period in folder type
  ' For StorageProvider (OneDrive) items, match inheritance on next part of string
  For i = 0 To UBFolderType
    If InStr(ArrFolderType(i),".") Then
      Key = Split(ArrFolderType(i),".")(0)
      ArrInherit(i) = oFolderTypes.Item(Key)
    Else
      If InStr(ArrFolderType(i),"StorageProvider") Then
        Key = Mid(ArrFolderType(i),16)
        ArrInherit(i) = oFolderTypes.Item(Key)
      Else
        ArrInherit(i) = 0
      End If
    End If
  Next

  ' For easier reference, copy data from scripting dictionary to variables
  Dict2Var(OnChange)

  ' Get Details view column headings
  
  ColFile = LangDir & Language & "\Columns-Win" & LWinVer & ".txt"
  ColData = oFSO.OpenTextFile(ColFile,ForReading,,Unicode).ReadAll
  ArrColData = Split(ColData,Z)
  ReDim ArrHTML2(UBound(ArrColData))
  
  ' Dynamically generate HTML page data
  BuildPages
  
  document.title = "WinSetView (" & ArrLang(1) & ")"
  lang.SelectedIndex = LangIndex
  Size1.SelectedIndex = SelectedSize1 - 8
  Size2.SelectedIndex = SelectedSize2 - 8
  IFace.SelectedIndex = 1
  
  ViewFile = LangDir & Language & "\ViewList.txt"

  If oFSO.FileExists(ViewFile) Then

  ' Get View menu data from file

    ViewData = oFSO.OpenTextFile(ViewFile,ForReading,,Unicode).ReadAll
    ArrView = Split(ViewData,Z)

    ' Populate the two View menus on the Options page

    For j = 0 To 8
      Set oOption = Document.createElement("Option")
      oOption.Text =  ArrView(j) 
      oOption.Value = j
      If j=0 Then oOption.Disabled = True
      tpv.Add(oOption)
      Set oOption = Document.createElement("Option")
      oOption.Text =  ArrView(j) 
      oOption.Value = j
    Next
      
    ' Populate each folder type setting (View, IconSize, GroupBy, SortBy, and Columns to display)

    For i = 0 To UBFolderType

      For j = 0 To 8
        Set oOption = Document.createElement("Option")
        oOption.Text =  ArrView(j) 
        oOption.Value = j
        If j=0 Then oOption.Disabled = True
        Document.GetElementByID(i & "View").Add(oOption)
      Next
      
      If ArrViewChoice(i)<>"" Then
        Document.GetElementByID(i & "View").SelectedIndex = ArrViewChoice(i)
      Else
        Document.GetElementByID(i & "View").SelectedIndex = 1
        ArrViewChoice(i) = 1
      End If

      If ArrIconSize(i)="" Then
        IconSizeUpdate(i)
      Else
        Document.GetElementByID(i & "IconSize").Value = ArrIconSize(i)
        Document.GetElementByID(i & "IconSize").Disabled = (ArrIconSize(i)="")
      End If

      If i>0 Then
        If ArrIconSize(i)="" Then x = "" Else x = " [" & ArrIconSize(i) & "]"
        Document.GetElementByID(i & "FTView").innerHTML = ArrView(0) & ": " & ArrView(ArrViewChoice(i)) & x
      End If

      If ArrGroupByOrder(i)="" Then ArrGroupByOrder(i)="+"
      If ArrGroupBy(i)="" Then
        Document.GetElementByID(i & "GroupBy").innerHTML = ArrView(9) 'None
      Else
        On Error Resume Next
        Document.GetElementByID(i & "GroupBy").innerHTML = ArrGroupByOrder(i) & Document.GetElementByID("L~" & ArrGroupBy(i)).Value
        On Error Goto 0
      End If

      If ArrSort1(i)="" Then ArrSort1(i) = "ItemNameDisplay"
      If ArrSort1Order(i)="" Then ArrSort1Order(i)="+"
      On Error Resume Next
      Sort1 = ArrSort1Order(i) & Document.GetElementByID("L~" & ArrSort1(i)).Value
      Sort2 = "": Sort3 = "": Sort4 = ""
      If ArrSort2(i)<>"" Then Sort2 = ArrSort2Order(i) & Document.GetElementByID("L~" & ArrSort2(i)).Value
      If ArrSort3(i)<>"" Then Sort3 = ArrSort3Order(i) & Document.GetElementByID("L~" & ArrSort3(i)).Value
      If ArrSort4(i)<>"" Then Sort4 = ArrSort4Order(i) & Document.GetElementByID("L~" & ArrSort4(i)).Value
      On Error Goto 0
      SortBy = Sort1
      If Sort2<>"" Then SortBy = SortBy & ", " & Sort2
      If Sort3<>"" Then SortBy = SortBy & ", " & Sort3
      If Sort4<>"" Then SortBy = SortBy & ", " & Sort4

      Document.GetElementByID(i & "SortBy").innerHTML = SortBy

      If i>0 Then
        Document.GetElementByID(i & "Inherit").Checked = ArrInheritFlag(i)
        Document.GetElementByID(i & "Include").Checked = ArrIncludeFlag(i)
        If ArrInheritFlag(i)="1" Then
          Document.GetElementByID(i & "FTcontrols").Style.Display = "none"
        Else
          Document.GetElementByID(i & "FTView").Style.Display = "none"
        End If
      End If

      ArrColumnList = Split(ArrColumnLists(i),";")

      ColShow = ""

      For j = 0 To UBound(ArrColumnList)
        ArrColumnEntry = Split(ArrColumnList(j),",")
        If ArrColumnEntry(0)="0" Then
          If ColShow<>"" Then ColShow = ColShow & " | "
          ColShowColor(ArrColumnEntry(2))
          On Error Resume Next
          ColShow = ColShow & "<span class=" & x & ">" & Document.GetElementByID("L~" & ArrColumnEntry(2)).Value & "</span>"
          On Error Goto 0
        End If
      Next

      Document.GetElementByID(i & "ColShow").innerHTML = ColShow
      UpdateFDView(i)
 
    Next

  End If

  ' Get list of fonts from file

  If Not oFSO.FileExists(FontsFile) Then
      ArrFonts = Array(SelectedFont1,SelectedFont2)
  Else
    Fonts = oFSO.OpenTextFile(FontsFile,ForReading,,Ansi).ReadAll
    ArrFonts = Split(Fonts,Z)
  End If

  ' Populate font menu

  For i = 0 To UBound(ArrFonts)
    Set oOption1 = Document.createElement("Option")
    Set oOption2 = Document.createElement("Option")
    If ArrFonts(i)<>"" Then
      oOption1.Text = ArrFonts(i) 
      oOption2.Text = ArrFonts(i) 
      oOption1.Value = ArrFonts(i)
      oOption2.Value = ArrFonts(i)
      Font1.Add(oOption1)
      Font2.Add(oOption2)
      If SelectedFont1=ArrFonts(i) Then Font1.SelectedIndex = i
      If SelectedFont2=ArrFonts(i) Then Font2.SelectedIndex = i
    End If
  Next

  ' Create Theme Select element with translated labels
  For i = -1 To UBound(ArrThemes)
    Set oOption = Document.createElement("Option")
    If i=-1 Then
      oOption.Disabled = True
      oOption.Text = ArrLang(37) 
      oOption.Value = ArrLang(37)
    Else
      x = Replace(Replace(ArrThemes(i),"Light",ArrLang(38)),"Dark",ArrLang(39))
      oOption.Text = x 
      oOption.Value = ArrThemes(i) 
    End If
    Theme.Add(oOption)
  Next

  ' Copy remaining settings from scripting dictionary to the HTML pages
  Dict2Dom
  
  ' Update page elements to selected font and size
  UpdateFont1

  If KeepAdvanced Then IFace.SelectedIndex = 2

  ' Hide or unhide controls based on Reset checkbox
  OptionReset
  
  ' Hide Restore button if no backup files
  If Not rbshow Then rb.Style.Display = "None"
  
  SetInterface
  
  SetTheme

  xschange

End Sub

Sub ColShowColor(e)
  x = "X1"
  If e="Search.Rank" Then x = "X2"
  If oSearchOnly.Exists(e) Then
    If so.Checked Then x = "X4" Else x = "X3"
  End If
  If HighContrast Then x = x & "HC"
End Sub

' Get preferred font for the selected language and Windows version

Sub GetFontFromFile
  FontFile = ""
  F1 = LangDir & Language & "\Font-Win" & WinVer & ".txt"
  F2 = LangDir & Language & "\Font.txt"
  If oFSO.FileExists(F1) Then
    FontFile = F1
  Else
    If oFSO.FileExists(F2) Then FontFile = F2
  End If
  If FontFile<>"" Then
    Set oFile = oFSO.OpenTextFile(FontFile,ForReading,,Ansi)
    SelectedFont1 = oFile.ReadLine
    oFile.Close
  End If
End Sub

' Populate variables from settings dictionary object

Sub Dict2Var(OnChange)

  If OnChange Then
    GetFontFromFile
  Else
    SelectedFont1 = "Segoe UI": SelectedSize1 = "11": SelectedFont2 = "Consolas": SelectedSize2 = "10"
    GetFontFromFile
    If oSettings.Exists("[Options]Font1") Then SelectedFont1 = oSettings.Item("[Options]Font1")
    If oSettings.Exists("[Options]Font2") Then SelectedFont2 = oSettings.Item("[Options]Font2")
    If oSettings.Exists("[Options]Size1") Then SelectedSize1 = oSettings.Item("[Options]Size1")
    If oSettings.Exists("[Options]Size2") Then SelectedSize2 = oSettings.Item("[Options]Size2")
  End If

  For i = 0 To UBFolderType
    ArrViewChoice(i) = oSettings.Item("[" & ArrFolderType(i) & "]View")
    ArrIconSize(i) = oSettings.Item("[" & ArrFolderType(i) & "]IconSize")
    ArrColumnLists(i) = oSettings.Item("[" & ArrFolderType(i) & "]ColumnList")
    ArrGroupBy(i) = oSettings.Item("[" & ArrFolderType(i) & "]GroupBy")
    ArrGroupByOrder(i) = oSettings.Item("[" & ArrFolderType(i) & "]GroupByOrder")
    ArrFD(i) = oSettings.Item("[" & ArrFolderType(i) & "]FileDialogOption")
    ArrFDV(i) = oSettings.Item("[" & ArrFolderType(i) & "]FileDialogView")
    ArrFDG(i) = oSettings.Item("[" & ArrFolderType(i) & "]FileDialogNG")
    ArrInheritFlag(i) = oSettings.Item("[" & ArrFolderType(i) & "]Inherit")
    ArrIncludeFlag(i) = oSettings.Item("[" & ArrFolderType(i) & "]Include")
    If ArrColumnLists(i)="" Then ArrColumnLists(i) = "0,34,ItemNameDisplay"
    If ArrInheritFlag(i)="" Then ArrInheritFlag(i) = "1"
    If ArrIncludeFlag(i)="" Then
      If ArrFolderType(i)="FileItemAPIs" Then
        ArrIncludeFlag(i) = "0"
      Else
        ArrIncludeFlag(i) = "1"
      End If
    End If
    ArrSort1Order(i) = "+": ArrSort1(i) = "ItemNameDisplay"
    ArrSort2Order(i) = "+": ArrSort2(i) = ""
    ArrSort3Order(i) = "+": ArrSort3(i) = ""
    ArrSort4Order(i) = "+": ArrSort4(i) = ""
    SortBy = oSettings.Item("[" & ArrFolderType(i) & "]SortBy")
    ArrSortBy = Split(SortBy,";")
    ReDim Preserve ArrSortBy(3)
    ArrSort1Order(i) = Left(ArrSortBy(0),1): ArrSort1(i) = Mid(ArrSortBy(0),2)
    ArrSort2Order(i) = Left(ArrSortBy(1),1): ArrSort2(i) = Mid(ArrSortBy(1),2)
    ArrSort3Order(i) = Left(ArrSortBy(2),1): ArrSort3(i) = Mid(ArrSortBy(2),2)
    ArrSort4Order(i) = Left(ArrSortBy(3),1): ArrSort4(i) = Mid(ArrSortBy(3),2)
    If ArrSort1Order(i)<>"+" And ArrSort1Order(i)<>"-" Then ArrSort1(i) = ArrSort1Order(i) & ArrSort1(i): ArrSort1Order(i) = "+"
    If ArrSort2Order(i)<>"+" And ArrSort2Order(i)<>"-" Then ArrSort2(i) = ArrSort2Order(i) & ArrSort2(i): ArrSort2Order(i) = "+"
    If ArrSort3Order(i)<>"+" And ArrSort3Order(i)<>"-" Then ArrSort3(i) = ArrSort3Order(i) & ArrSort3(i): ArrSort3Order(i) = "+"
    If ArrSort4Order(i)<>"+" And ArrSort4Order(i)<>"-" Then ArrSort4(i) = ArrSort4Order(i) & ArrSort4(i): ArrSort4Order(i) = "+"
  Next
End Sub

' Populate HTML pages directly from scripting dictionary for some settings

Sub Dict2Dom
  wd.checked = SetInitial("[Options]Reset",0)
  se.Checked = SetInitial("[Options]ShowExt",1)
  cv.Checked = SetInitial("[Options]CompView",1)
  gn.checked = SetInitial("[Options]Generic",0)
  so.Checked = SetInitial("[Options]SearchOnly",1)
  vf.Checked = SetInitial("[Options]SetVirtualFolders",1)
  ka.checked = SetInitial("[Options]KeepViews",0)
  nf.checked = SetInitial("[Options]NoFolderThumbs",0)
  ua.checked = SetInitial("[Options]UnhideAppData",0)
  cm.checked = SetInitial("[Options]ClassicContextMenu",0)
  si.checked = SetInitial("[Options]NoSearchInternet",0)
  sh.checked = SetInitial("[Options]NoSearchHighlights",0)
  tp.checked = SetInitial("[Options]ThisPCoption",1)
  tpg.Checked = SetInitial("[Options]ThisPCNG",0)
  tpv.SelectedIndex = SetInitial("[Options]ThisPCView",3)
  xs.SelectedIndex = SetInitial("[Options]Scroll",1)
  IFace.SelectedIndex = SetInitial("[Options]Interface",1)
  Theme.SelectedIndex = SetInitial("[Options]ThemeIndex",1)
End Sub

Function SetInitial(DictEntry,Default)
  SetInitial = oSettings.Item(DictEntry)
  If SetInitial="" Then SetInitial = Default
  SetInitial = Int(SetInitial)
End Function

' Load Settings button

Sub LoadSettings
  If RestartFileDialog() Then
    DialogArgs = """" & FileDialogExe & """" & " Open Multi ""*.ini|*.ini"" """ & AppData & """"
    File = ShowModalDialog(FileDialogHTA,DialogArgs,DialogOptions)
    lb.blur
    If File="" Or File="?" Then Exit Sub
    KeepAdvanced = True
    Set oSettings = Ini2Dict(File)
    SetLang
    UpdateLang False
  End If
End Sub

' Save Settings button

Sub SaveSettings
  If RestartFileDialog() Then
    DialogArgs = """" & FileDialogExe & """" & " Save ""*.ini|*.ini"" """ & AppData & """"
    File = ShowModalDialog(FileDialogHTA,DialogArgs,DialogOptions)
    sb.blur
    If File="" Or File="?" Then Exit Sub
    Var2Ini(File)
  End If
End Sub

' Restore button

Sub Restore
  If RestartFileDialog() Then
    DialogArgs = """" & FileDialogExe & """" & " Open Multi ""*.reg|*.reg"" """ & Backup & """"
    File = ShowModalDialog(FileDialogHTA,DialogArgs,DialogOptions)
    rb.blur
    If File="" Or File="?" Then Exit Sub
    RunScript File
  End If
End Sub

Function RestartFileDialog()
  DialogOptions = "dialogWidth:1px; dialogHeight:1px; dialogLeft:999999999; dialogTop:999999999"
  RestartFileDialog = True
  oWSH.Run "TaskKill /im FileDialog.exe /f",0,True
  If Not oFSO.FileExists(FileDialogExe) Then
    MsgBox "File missing: " & FileDialogExe,vbCritical,"Error"
    RestartFileDialog = False
  End If
  If Not oFSO.FileExists(FileDialogHTA) Then
    MsgBox "File missing: " & FileDialogHTA,vbCritical,"Error"
    RestartFileDialog = False
  End If
End Function

' Write vars to INI file

Sub Var2Ini(INIFile)
  Set oFile = oFSO.OpenTextFile(INIFile,ForWriting,True,Ansi)
  oFile.Write "[Options]" & Z
  oFile.Write "ThemeIndex=" & Theme.SelectedIndex & Z
  oFile.Write "Language=" & Language & Z
  oFile.Write "Font1=" & Font1.Options(Font1.SelectedIndex).Value & Z
  oFile.Write "Font2=" & Font2.Options(Font2.SelectedIndex).Value & Z
  oFile.Write "Size1=" & Size1.Options(Size1.SelectedIndex).Value & Z
  oFile.Write "Size2=" & Size2.Options(Size2.SelectedIndex).Value & Z
  oFile.Write "Scroll=" & xs.SelectedIndex & Z
  oFile.Write "Interface=" & IFace.SelectedIndex & Z
  oFile.Write "Reset=" & Abs(Int(wd.Checked)) & Z
  oFile.Write "ShowExt=" & Abs(Int(se.Checked)) & Z
  oFile.Write "CompView=" & Abs(Int(cv.Checked)) & Z
  oFile.Write "Generic=" & Abs(Int(gn.Checked)) & Z
  oFile.Write "SearchOnly=" & Abs(Int(so.Checked)) & Z
  oFile.Write "SetVirtualFolders=" & Abs(Int(vf.Checked)) & Z
  oFile.Write "KeepViews=" & Abs(Int(ka.Checked)) & Z
  oFile.Write "NoFolderThumbs=" & Abs(Int(nf.Checked)) & Z
  oFile.Write "UnhideAppData=" & Abs(Int(ua.Checked)) & Z
  oFile.Write "ClassicContextMenu=" & Abs(Int(cm.Checked)) & Z
  oFile.Write "NoSearchInternet=" & Abs(Int(si.Checked)) & Z
  oFile.Write "NoSearchHighlights=" & Abs(Int(sh.Checked)) & Z
  On Error Resume Next
  x = oWSH.RegRead("HKCU\Software\Classes\Local Settings\Software\Microsoft\Windows\Shell\Bags\AllFolders\Shell\Logo")
  On Error Goto 0
  ResetThumbs=0
  If x<>"none" And nf.Checked Then ResetThumbs=1
  If x="none" And Not nf.Checked Then ResetThumbs=1
  oFile.Write "ResetThumbs=" & ResetThumbs & Z
  oFile.Write "ThisPCoption=" & Abs(Int(tp.Checked)) & Z
  oFile.Write "ThisPCView=" & tpv.SelectedIndex & Z
  oFile.Write "ThisPCNG=" & Abs(Int(tpg.Checked)) & Z
  For i = 0 To UBFolderType
    oFile.Write "[" & ArrFolderType(i) & "]" & Z
    If i>0 Then 
      oFile.Write "GUID=" & ArrFolderTypeGUID(i) & Z
      oFile.Write "Include=" & ArrIncludeFlag(i) & Z
      oFile.Write "Inherit=" & ArrInheritFlag(i) & Z
    End If
    oFile.Write "View=" & Document.GetElementByID(i & "View").SelectedIndex & Z
    oFile.Write "IconSize=" & Document.GetElementByID(i & "IconSize").Value & Z
    oFile.Write "ColumnList=" & ArrColumnLists(i) & Z
    oFile.Write "GroupBy=" & ArrGroupBy(i) & Z
    oFile.Write "GroupByOrder=" & ArrGroupByOrder(i) & Z
    oFile.Write "FileDialogOption=" & ArrFD(i) & Z
    oFile.Write "FileDialogView=" & ArrFDV(i) & Z
    oFile.Write "FileDialogNG=" & ArrFDG(i) & Z
    SortBy = ArrSort1Order(i) & ArrSort1(i)
    If ArrSort2(i)<>"" Then SortBy = SortBy & ";" & ArrSort2Order(i) & ArrSort2(i)
    If ArrSort3(i)<>"" Then SortBy = SortBy & ";" & ArrSort3Order(i) & ArrSort3(i)
    If ArrSort4(i)<>"" Then SortBy = SortBy & ";" & ArrSort4Order(i) & ArrSort4(i)
    oFile.Write "SortBy=" & SortBy & Z
  Next
  oFile.Close
End Sub

' Script for displaying synchronized views of registry before/after views

Sub Inspection(i)
  Key = "\Software\Microsoft\Windows\CurrentVersion\Explorer\FolderTypes\"
  oWSH.Run "Reg Export " & "HKLM" & Key & ArrFolderTypeGUID(i) & " " & Q & TempFile & Q & " /y",0,True
  Txt1Data = oFSO.OpenTextFile(TempFile,ForReading,,Unicode).ReadAll
  Txt1.Value = Txt1Data

  oFSO.DeleteFile TempFile
  oWSH.Run "Reg Export " & "HKCU" & Key & ArrFolderTypeGUID(i) & " " & Q & TempFile & Q & " /y",0,True
  If oFSO.FileExists(TempFile) Then
    Txt2Data = oFSO.OpenTextFile(TempFile,ForReading,,Unicode).ReadAll
    Txt2.Value = Txt2Data
  End If

  YScrollPos = Document.documentElement.ScrollTop
  Pg1.Style.Display = "none"
  Pg2.Style.Display = "none"
  UpdateFont2
  UpdateSize2
  Document.documentElement.Style.overflow = "hidden"
  HeadHeight = P3hd.ScrollHeight + 50
  AvailHeight = Document.documentElement.clientHeight - HeadHeight
  y = Int(AvailHeight / 2)
  Txt1.Style.Height = y
  Txt2.Style.Height = y
  Window.ScrollTo 0, 0
End Sub

Function Modal(Title,Args,DialogTop)
  If Not oFSO.FileExists(ModalHTA) Then
    MsgBox "File missing: " & ModalHTA,vbCritical,"Error"
  Else
    x = Abs(Int(Instr(LCase(sTheme),"dark")>0))
    If HighContrast Then x = 2
    DialogArgs = Title & ";" & SelectedFont1 & ";" & SelectedSize1 & ";" & Scale & ";" & x & ";" & Args
    x = Int(Document.documentElement.clientWidth/(BaseScale*16))
    x = Math.Min(x,Int(20*Scale))
    DialogOptions = "Scroll:No; DialogWidth:" & x & "em; dialogLeft:" & window.screenLeft+Int(2*Scale) & "; dialogTop:" & DialogTop
    Modal = ShowModalDialog(ModalHTA,DialogArgs,DialogOptions)
  End If
End Function

Sub FTOptions(i)
  RetVal = Modal(OptIcon, ArrLang(12) & ";" & ArrLang(25) & ";" & Join(ArrView,"|") & ";" & ArrFD(i) & ";" & ArrFDG(i) & ";" & ArrFDV(i), window.event.clientY)
  If RetVal<>"" Then
    TempArray = Split(RetVal,"|")
    ArrFD(i) = Int(TempArray(0))
    ArrFDV(i) = Int(TempArray(1))
    ArrFDG(i) = Int(TempArray(2))
  End If
  UpdateFDView(i)
  InheritUpdate(i)
End Sub

Sub UpdateFDView(i)
  If ArrFD(i)="1" Then
    LParen = "": RParen = ""
    If ArrFDG(i)="0" Then LParen = "(": RParen = ")"
    Document.GetElementByID(i & "FTDialogView").Style.Display = "block"
    Document.GetElementByID(i & "DialogView").innerHTML = LParen & ArrView(ArrFDV(i)) & RParen
  Else
    Document.GetElementByID(i & "FTDialogView").Style.Display = "none"
  End If
End Sub

Sub FTCopy(i)
  FTCopyIdx = i
End Sub

Sub FTPaste(i)
  If FTCopyIdx="" Then Exit Sub
  ArrViewChoice(i) = ArrViewChoice(FTCopyIdx)
  ArrIconSize(i) = ArrIconSize(FTCopyIdx)
  ArrColumnLists(i) = ArrColumnLists(FTCopyIdx)
  ArrGroupBy(i) = ArrGroupBy(FTCopyIdx)
  ArrGroupByOrder(i) = ArrGroupByOrder(FTCopyIdx)
  ArrFD(i) = ArrFD(FTCopyIdx)
  ArrFDV(i) = ArrFDV(FTCopyIdx)
  ArrFDG(i) = ArrFDG(FTCopyIdx)
  ArrSort1(i) = ArrSort1(FTCopyIdx)
  ArrSort1Order(i) = ArrSort1Order(FTCopyIdx)
  ArrSort2(i) = ArrSort2(FTCopyIdx)
  ArrSort2Order(i) = ArrSort2Order(FTCopyIdx)
  ArrSort3(i) = ArrSort3(FTCopyIdx)
  ArrSort3Order(i) = ArrSort3Order(FTCopyIdx)
  ArrSort4(i) = ArrSort4(FTCopyIdx)
  ArrSort4Order(i) = ArrSort4Order(FTCopyIdx)
  Document.GetElementByID(i & "View").SelectedIndex = ArrViewChoice(i)
  Document.GetElementByID(i & "IconSize").Value = ArrIconSize(i)
  Document.GetElementByID(i & "GroupBy").innerHTML = Document.GetElementByID(FTCopyIdx & "GroupBy").innerHTML
  Document.GetElementByID(i & "SortBy").innerHTML = Document.GetElementByID(FTCopyIdx & "SortBy").innerHTML
  Document.GetElementByID(i & "ColShow").innerHTML = Document.GetElementByID(FTCopyIdx & "ColShow").innerHTML
  UpdateFDView(i)
  InheritUpdate(i)
End Sub

' Display Options page

Sub Options
  YScrollPos = Document.documentElement.ScrollTop
  Pg1.Style.Display = "none"
  Pg4.Style.Display = "inline"
  OptionThisPC
End Sub

Sub Arrange
  GenColShow
  TempArray = Split(ColShowLabels,"|")
  DialogTop = window.screenTop+Int(fix2.ScrollHeight-(Scale*60))
  RetVal = Modal(ArrLang(36), ColShowLabels, DialogTop)
  If RetVal="" Then Exit Sub
  TempArray = Split(RetVal,"|")
  For j = 0 To UBound(TempArray)
    id = Split(ArrData(TempArray(j)),";")(4)
    Document.GetElementByID("N~" & id).Value = j
  Next
  GenColShow
  If Document.GetElementByID("N~ItemNameDisplay").Value<>0 Then
    RetVal = Modal(Warning, ArrLang(43), DialogTop)
  End If
End Sub

' Save settings and run WinSetView.ps1

Sub Submit
  Var2Ini(INIFile)
  RunScript INIFile
End Sub

' Run WinSetView.ps1 with supplied parameter (INI or REG filename)

Sub RunScript(Param)
  x = ""
  If Right(Param,3)="ini" Then
    If window.event.altKey Then x = "-NoExit"
  End If
  CmdLine = "Powershell.exe " & x & " -NoLogo -ExecutionPolicy Bypass -File .\WinSetView.ps1 " & Chr(34) & Param & Chr(34)
  oWSH.Run CmdLine,1,False
  Window.Close
End Sub

' Set values for column selection page

Sub SelectColumns(i)
  FTindex = i

  'Reset everything
  For i = 0 To UBound(ArrColData)
    If ArrColData(i)<>"" Then
      ArrColDataItem = Split(ArrColData(i),";")
      C1 = ArrColDataItem(1): C2 = ArrColDataItem(0)
      Pidx = "P~" & C1: Widx = "W~" & C1: Nidx = "N~" & C1: Gidx = "G~" & C1: S1idx = "S1~" & C1: S2idx = "S2~" & C1: S3idx = "S3~" & C1: S4idx = "S4~" & C1: Midx = "M~" & C1
      Document.GetElementByID(Nidx).Value = ""
      Document.GetElementByID(Widx).Value = ""
      If C1="ItemNameDisplay" Then
        Document.GetElementByID(Pidx).Checked = True
        Document.GetElementByID(Midx).Checked = True
        Document.GetElementByID(Pidx).Disabled = True
        Document.GetElementByID(Midx).Disabled = True
      Else
        Document.GetElementByID(Pidx).Checked = False
        Document.GetElementByID(Midx).Checked = False
      End If
      If ArrGroupBy(FTindex)="" Then
        GroupBy(i).Style.visibility = "hidden"
        Document.GetElementByID(Gidx).Checked = False
      Else
        GroupBy(i).Style.visibility = "visible"
        If ArrGroupBy(FTindex)=C1 Then Document.GetElementByID(Gidx).Checked = True
      End If
      If ArrSort1(FTindex)=C1 Then Document.GetElementByID(S1idx).Checked = True
      If ArrSort2(FTindex)="" Then 
        RBS2(i).Style.visibility = "hidden"
        Document.GetElementByID(S2idx).Checked = False
      Else
        RBS2(i).Style.visibility = "visible"
        If ArrSort2(FTindex)=C1 Then Document.GetElementByID(S2idx).Checked = True
      End If
      If ArrSort3(FTindex)="" Then 
        RBS3(i).Style.visibility = "hidden"
        Document.GetElementByID(S3idx).Checked = False
      Else
        RBS3(i).Style.visibility = "visible"
        If ArrSort3(FTindex)=C1 Then Document.GetElementByID(S3idx).Checked = True
      End If
      If ArrSort4(FTindex)="" Then 
        RBS4(i).Style.visibility = "hidden"
        Document.GetElementByID(S4idx).Checked = False
      Else
        RBS4(i).Style.visibility = "visible"
        If ArrSort4(FTindex)=C1 Then Document.GetElementByID(S4idx).Checked = True
      End If
    End If
  Next

  If ArrGroupBy(FTindex)="" Then
    cbg.Value = "X"
  Else
    If ArrGroupByOrder(FTindex)="+" Then cbg.Value = GrpUpArrow Else cbg.Value = GrpDnArrow
  End If

  If ArrSort1Order(FTindex)="+" Then cbs1.Value = UpArrow Else cbs1.Value = DnArrow
  If ArrSort2Order(FTindex)="+" Then cbs2.Value = UpArrow Else cbs2.Value = DnArrow
  If ArrSort3Order(FTindex)="+" Then cbs3.Value = UpArrow Else cbs3.Value = DnArrow
  If ArrSort4Order(FTindex)="+" Then cbs4.Value = UpArrow Else cbs4.Value = DnArrow
  
  If ArrSort2(FTindex)="" Then cbs2.Value = "X"
  If ArrSort3(FTindex)="" Then cbs3.Value = "X"
  If ArrSort4(FTindex)="" Then cbs4.Value = "X"

  ArrColumnList = Split(ArrColumnLists(FTindex),";")

  Order = 0
  For i = 0 To UBound(ArrColumnList)
    ArrColumnEntry = Split(ArrColumnList(i),",")
    C1 = ArrColumnEntry(2)
    Pidx = "P~" & C1: Widx = "W~" & C1: Nidx = "N~" & C1: Midx = "M~" & C1: 
    Show = ArrColumnEntry(0)
    On Error Resume Next
    Document.GetElementByID(Pidx).Checked = True
    If Show="0" Then
      Document.GetElementByID(Nidx).Value = Order
      Document.GetElementByID(Midx).Checked = True
    End If
    Document.GetElementByID(Widx).Value = ArrColumnEntry(1)
    On Error Goto 0
    Order = Order + 1
  Next

  CheckCount = i
  cs.innerHTML = Document.GetElementByID(FTindex & "ColShow").innerHTML
  SetColShowColor
  P2tt.innerHTML = ArrFolderTypeLang(FTindex)

  tab2.Style.LineHeight = Int(Scale * 10) & "pt"

  YScrollPos = Document.documentElement.ScrollTop
  Pg1.Style.Display = "none"
  Pg2.Style.Display = "inline"
  Pg3.Style.Display = "none"
  tcin.Style.MarginTop = fix2.ScrollHeight - Int(Scale * 20)
  Window.ScrollTo 0, 0
End Sub

' Generate column heading list based with click order data included

Sub GenColShow

  j = -1
  For i = 0 To UBound(ArrColData)
    If ArrColData(i)<>"" Then
      ArrColDataItem = Split(ArrColData(i),";")
      C1 = ArrColDataItem(1): C2 = ArrColDataItem(0)
      Pidx = "P~" & C1: Widx = "W~" & C1: Nidx = "N~" & C1
      Show = "": sOrder = ""
      sOrder = Document.GetElementByID(Nidx).Value
      If sOrder<>"" Then
        Show = "0"
      Else
        If Document.GetElementByID(Pidx).Checked Then Show = "1"
      End If
      If Show<>"" Then
        ColWid = Document.GetElementByID(Widx).Value
        j = j + 1
        ArrData(j) = Show & ";" & Right("000" & sOrder, 3) & ";" & ColWid & ";" & ArrColData(i)
      End If
    End If
  Next

  LastColSel = j

  ' Sort list
  
  x = LastColSel - 1
  For i = x To 0 Step -1
    For j= 0 To i
    If ArrData(j)>ArrData(j+1) Then
      y = ArrData(j+1)
      ArrData(j+1) = ArrData(j)
      ArrData(j) = y
    End If
    Next
  Next

  ' Create list to be displayed

  Data = ""
  ColShow = ""
  ColShowLabels = ""
  For i = 0 To LastColSel
    ArrColumnEntry = Split(ArrData(i),";")
    If Data<>"" Then Data = Data & ";"
    Data = Data & ArrColumnEntry(0) & "," & ArrColumnEntry(2) & "," & ArrColumnEntry(4)
    If ArrColumnEntry(0)="0" Then
      If ColShow<>"" Then ColShow = ColShow & " | "
      If ColShowLabels<>"" Then ColShowLabels = ColShowLabels & "|"
      ColShowColor(ArrColumnEntry(4))
      ColShow = ColShow & "<span class=" & x & ">" & ArrColumnEntry(3) & "</span>"
      ColShowLabels = ColShowLabels & ArrColumnEntry(3)
    End If
  Next

  ' Display column heading list
  cs.innerHTML = ColShow
  SetColShowColor

End Sub

' Return values from column selection page

Sub P2Exit

  GenColShow
  
  ArrColumnLists(FTindex) = Data
  Document.GetElementByID(FTindex & "ColShow").innerHTML = ColShow
  SetColShowColor
  
  If cbg.Value="X" Then ArrGroupBy(FTindex)=""
  If ArrGroupBy(FTindex)="" Then
    Document.GetElementByID(FTindex & "GroupBy").innerHTML = ArrView(9) 'None
  Else
    On Error Resume Next
    Document.GetElementByID(FTindex & "GroupBy").innerHTML = ArrGroupByOrder(FTindex) & Document.GetElementByID("L~" & ArrGroupBy(FTindex)).Value
    On Error Goto 0
  End If

  If cbs2.Value="X" Then ArrSort2(FTindex)=""
  If cbs3.Value="X" Then ArrSort3(FTindex)=""
  If cbs4.Value="X" Then ArrSort4(FTindex)=""

  On Error Resume Next
  SortBy = ArrSort1Order(FTindex) & Document.GetElementByID("L~" & ArrSort1(FTindex)).Value
  If ArrSort2(FTindex)<>"" Then SortBy = SortBy & ", " & ArrSort2Order(FTindex) & Document.GetElementByID("L~" & ArrSort2(FTindex)).Value
  If ArrSort3(FTindex)<>"" Then SortBy = SortBy & ", " & ArrSort3Order(FTindex) & Document.GetElementByID("L~" & ArrSort3(FTindex)).Value
  If ArrSort4(FTindex)<>"" Then SortBy = SortBy & ", " & ArrSort4Order(FTindex) & Document.GetElementByID("L~" & ArrSort4(FTindex)).Value
  On Error Goto 0
  Document.GetElementByID(FTindex & "SortBy").innerHTML = SortBy
  
  InheritUpdate(i)

  Back2Pg1

End Sub

' Return to main page

Sub Back2Pg1
  Pg4.Style.Display = "none"
  Pg3.Style.Display = "none"
  Pg2.Style.Display = "none"
  Pg1.Style.Display = "inline"
  Document.documentElement.Style.overflow = "auto"
  Window.ScrollTo 0, YScrollPos
End Sub

' Event handlers for main page:

Sub OptionReset
  If wd.Checked Then
    FTHide = True
    Sec1.Style.visibility = "hidden"
    Sec4.Style.visibility = "hidden"
    lb.Style.visibility = "hidden"
    sb.Style.visibility = "hidden"
    ao.Style.visibility = "hidden"
  Else
    FTHide = False
    Sec1.Style.visibility = "visible"
    Sec4.Style.visibility = "visible"
    If IFace.SelectedIndex=2 Then
      lb.Style.visibility = "visible"
      sb.Style.visibility = "visible"
      ao.Style.visibility = "visible"
    End If
  End If
  For i = 1 To UBFolderType
    FTHideUnHide i,FTHide
  Next
End Sub

Sub OptionSearchOnly
  Set oElements = Document.GetElementsByTagName("span")
  x = ""
  If HighContrast Then x = x & "HC"
  For Each oElement in oElements
    If oElement.ClassName="X3" & x Then
      oElement.ClassName = "X4" & x
    Else
      If oElement.ClassName="X4" & x Then oElement.ClassName = "X3" & x
    End If
  Next
  SetColShowColor
End Sub

Sub OptionThisPC
  If tp.Checked=True Then
    otp.Style.Display = "block"
  Else
    otp.Style.Display = "none"
  End If
End Sub

Sub IconSizeUpdate(i)

  ArrIconSize(i) = ""
  
  Select Case ArrViewChoice(i)
    Case "5"
     ArrIconSize(i) = "16"
    Case "6"
     ArrIconSize(i) = "48"
    Case "7"
     ArrIconSize(i) = "96"
    Case "8"
     ArrIconSize(i) = "256"
  End Select
  
  Set o = Document.GetElementByID(i & "IconSize")
  o.Value = ArrIconSize(i)
  If ArrIconSize(i)="" Then
    o.Disabled = True
    o.Style.backgroundColor = oThemes.Item(sTheme & "InputDisabledBackground")
  Else
    o.Disabled = False
    o.Style.backgroundColor = oThemes.Item(sTheme & "InputBackground")
  End If
End Sub

Sub ViewChange(i)
  ArrViewChoice(i) = Document.GetElementByID(i & "View").Value
  IconSizeUpdate(i)
  InheritUpdate(i)
End Sub

Sub IconSizeChange(i)
  ArrIconSize(i) = Document.GetElementByID(i & "IconSize").Value
  InheritUpdate(i)
End Sub

Sub InheritChange(i)
  ArrInheritFlag(i) = Abs(Int(Document.GetElementByID(i & "Inherit").Checked))
  If ArrInheritFlag(i)="1" Then
    Document.GetElementByID(i & "FTcontrols").Style.Display = "none"
    Document.GetElementByID(i & "FTView").Style.Display = "block"
  Else
    Document.GetElementByID(i & "FTcontrols").Style.Display = "block"
    Document.GetElementByID(i & "FTView").Style.Display = "none"
  End If
  InheritUpdate(i)
End Sub

Sub InheritUpdate(i)
  For i = 1 To UBFolderType
    If Document.GetElementByID(i & "Inherit").Checked Then
      ArrViewChoice(i) = ArrViewChoice(ArrInherit(i))
      ArrIconSize(i) = ArrIconSize(ArrInherit(i))
      ArrColumnLists(i) = ArrColumnLists(ArrInherit(i))
      ArrGroupBy(i) = ArrGroupBy(ArrInherit(i))
      ArrGroupByOrder(i) = ArrGroupByOrder(ArrInherit(i))
      ArrFD(i) = ArrFD(ArrInherit(i))
      ArrFDV(i) = ArrFDV(ArrInherit(i))
      ArrFDG(i) = ArrFDG(ArrInherit(i))
      ArrSort1(i) = ArrSort1(ArrInherit(i))
      ArrSort1Order(i) = ArrSort1Order(ArrInherit(i))
      ArrSort2(i) = ArrSort2(ArrInherit(i))
      ArrSort2Order(i) = ArrSort2Order(ArrInherit(i))
      ArrSort3(i) = ArrSort3(ArrInherit(i))
      ArrSort3Order(i) = ArrSort3Order(ArrInherit(i))
      ArrSort4(i) = ArrSort4(ArrInherit(i))
      ArrSort4Order(i) = ArrSort4Order(ArrInherit(i))

      Document.GetElementByID(i & "View").SelectedIndex = ArrViewChoice(i)
      Document.GetElementByID(i & "IconSize").Value = ArrIconSize(i)
      If ArrIconSize(i)="" Then x = "" Else x = " [" & ArrIconSize(i) & "]"
      Document.GetElementByID(i & "FTView").innerHTML = ArrView(0) & ": " & ArrView(ArrViewChoice(i)) & x
      Document.GetElementByID(i & "GroupBy").innerHTML = Document.GetElementByID(ArrInherit(i) & "GroupBy").innerHTML
      Document.GetElementByID(i & "SortBy").innerHTML = Document.GetElementByID(ArrInherit(i) & "SortBy").innerHTML
      Document.GetElementByID(i & "ColShow").innerHTML = Document.GetElementByID(ArrInherit(i) & "ColShow").innerHTML

      UpdateFDView(i)
    End If
  Next
End Sub

Sub IncludeChange(i)
  ArrIncludeFlag(i) = Abs(Int(Document.GetElementByID(i & "Include").Checked))
  FTHideUnHide i,False
  InheritUpdate(i)
End Sub

Sub IncludeAll
  For i = 1 To UBFolderType
    ArrIncludeFlag(i) = Abs(Int(ica.Checked))
    FTHideUnHide i,False
    InheritUpdate(i)
  Next
End Sub

' Event handlers for column selection page buttons:

Sub GroupByClick
  x = cbg.Value
  Select Case x
    Case GrpUpArrow
      cbg.Value = GrpDnArrow
      ArrGroupByOrder(FTindex) = "-"
    Case GrpDnArrow
      cbg.Value = "X"
      For i = 0 To UBound(ArrColData)
        GroupBy(i).Style.visibility = "hidden"
      Next
    Case "X"
      cbg.Value = GrpUpArrow
      ArrGroupByOrder(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        GroupBy(i).Style.visibility = "visible"
      Next
  End Select
End Sub

Sub SortBy1Click
  If cbs1.Value=UpArrow Then
    cbs1.Value = DnArrow
    ArrSort1Order(FTindex) = "-"
  Else
    cbs1.Value = UpArrow
    ArrSort1Order(FTindex) = "+"
  End If
End Sub

Sub SortBy2Click
  x = cbs2.Value
  Select Case x
    Case UpArrow
      cbs2.Value = DnArrow
      ArrSort2Order(FTindex) = "-"
    Case DnArrow
      cbs2.Value = "X"
      ArrSort2Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS2(i).Style.visibility = "hidden"
      Next
    Case "X"
      cbs2.Value = UpArrow
      ArrSort2Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS2(i).Style.visibility = "visible"
      Next
  End Select
End Sub

Sub SortBy3Click
  x = cbs3.Value
  Select Case x
    Case UpArrow
      cbs3.Value = DnArrow
      ArrSort3Order(FTindex) = "-"
    Case DnArrow
      cbs3.Value = "X"
      ArrSort3Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS3(i).Style.visibility = "hidden"
      Next
    Case "X"
      cbs3.Value = UpArrow
      ArrSort3Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS3(i).Style.visibility = "visible"
      Next
  End Select
End Sub

Sub SortBy4Click
  x = cbs4.Value
  Select Case x
    Case UpArrow
      cbs4.Value = DnArrow
      ArrSort4Order(FTindex) = "-"
    Case DnArrow
      cbs4.Value = "X"
      ArrSort4Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS4(i).Style.visibility = "hidden"
      Next
    Case "X"
      cbs4.Value = UpArrow
      ArrSort4Order(FTindex) = "+"
      For i = 0 To UBound(ArrColData)
        RBS4(i).Style.visibility = "visible"
      Next
  End Select
End Sub

' Event handlers for column (property) selection page items:
' The id is the property name

Sub CBMClick(id)
  If Document.GetElementByID("M~" & id).Checked Then
    If Not Document.GetElementByID("P~" & id).Checked Then
      If CheckCount=21 Then
        Document.GetElementByID("M~" & id).Checked = False
        MsgBox "☑ > 21",vbCritical
        Exit Sub
      Else
        Document.GetElementByID("P~" & id).Checked = True
        CheckCount = CheckCount + 1
      End If
    End If
    Order = Order + 1
    Document.GetElementByID("N~" & id).Value = Order
  Else
    Document.GetElementByID("N~" & id).Value = ""
  End If
  GenColShow
End Sub

Sub CBPClick(id)
  If Document.GetElementByID("P~" & id).Checked Then
    If CheckCount=21 Then
      Document.GetElementByID("P~" & id).Checked = False
      MsgBox "☑ > 21",vbCritical
      Exit Sub
    Else
      CheckCount = CheckCount + 1
    End If
  Else
    CheckCount = CheckCount - 1
    If Document.GetElementByID("M~" & id).Checked Then
      Document.GetElementByID("M~" & id).Checked = False
      Document.GetElementByID("N~" & id).Value = ""
      GenColShow
    End If
  End If
End Sub

Sub RBGClick(id)
  ArrGroupBy(FTindex) = Document.GetElementByID("G~" & id).Value
End Sub

Sub RBS1Click(id)
  ArrSort1(FTindex) = Document.GetElementByID("S1~" & id).Value
End Sub

Sub RBS2Click(id)
  ArrSort2(FTindex) = Document.GetElementByID("S2~" & id).Value
End Sub

Sub RBS3Click(id)
  ArrSort3(FTindex) = Document.GetElementByID("S3~" & id).Value
End Sub

Sub RBS4Click(id)
  ArrSort4(FTindex) = Document.GetElementByID("S4~" & id).Value
End Sub

Sub WidthClick
  DialogTop = window.event.clientY - Int(Scale * 25)
  If Modal(LRArrow, ArrLang(26), DialogTop)="1" Then
    For i = 0 To UBound(ArrColData)
      If ArrColData(i)<>"" Then
        ArrColDataItem = Split(ArrColData(i),";")
        Widx = "W~" & ArrColDataItem(1)
        Document.GetElementByID(Widx).Value = ""
      End If
    Next
  End If
End Sub

Sub View1Click
  DialogTop = window.event.clientY - Int(Scale * 25)
  If Modal(DetailsIcon, ArrLang(26), DialogTop)="1" Then
    For i = 0 To UBound(ArrColData)
      If ArrColData(i)<>"" Then
        ArrColDataItem = Split(ArrColData(i),";")
        C1 = ArrColDataItem(1)
        Pidx = "P~" & C1: Midx = "M~" & C1: Nidx = "N~" & C1
        If C1<>"ItemNameDisplay" And Document.GetElementByID(Midx).Checked=True Then
          Document.GetElementByID(Midx).Checked = False
          Document.GetElementByID(Nidx).Value = ""
        End If
      End If
    Next
    cs.innerHTML = Split(ArrColData(0),";")(0)
  End If
End Sub

Sub View2Click
  Dim ClearAll: ClearAll = False
  DialogTop = window.event.clientY - Int(Scale * 25)
  If Modal(RightClickIcon, ArrLang(26), DialogTop)="1" Then
    If window.event.altKey Then ClearAll = True
    For i = 0 To UBound(ArrColData)
      If ArrColData(i)<>"" Then
        ArrColDataItem = Split(ArrColData(i),";")
        C1 = ArrColDataItem(1)
        Pidx = "P~" & C1: Midx = "M~" & C1: Nidx = "N~" & C1
        If C1<>"ItemNameDisplay" Then
          If ClearAll Then
            Document.GetElementByID(Pidx).Checked = False
            Document.GetElementByID(Midx).Checked = False
            Document.GetElementByID(Nidx).Value = ""
          Else
            If Document.GetElementByID(Midx).Checked = False Then
              If Document.GetElementByID(Pidx).Checked=True Then
                Document.GetElementByID(Pidx).Checked = False
                CheckCount = CheckCount - 1
              End If
            End If
          End If
        End If
      End If
    Next
    If ClearAll Then
      cs.innerHTML = Split(ArrColData(0),";")(0)
      CheckCount = 1
    End If
  End If
End Sub

Sub Txt1Scroll
  Txt2.ScrollTop = Txt1.ScrollTop
  Txt2.ScrollLeft = Txt1.ScrollLeft
End Sub

Sub Txt2Scroll
  Txt1.ScrollTop = Txt2.ScrollTop
  Txt1.ScrollLeft = Txt2.ScrollLeft
End Sub

' Set page elements to selected font and size

Sub UpdateFont1
  SelectedFont1 = Font1.Options(Font1.SelectedIndex).Value
  SelectedSize1 = Size1.Options(Size1.SelectedIndex).Value
  Scale = (SelectedSize1/10) * BaseScale
  
  Document.Body.Style.FontFamily = SelectedFont1
  Document.Body.Style.FontSize = SelectedSize1 & "pt"

  Set oElements = Document.GetElementsByTagName("input")
  For Each oElement in oElements
    If oElement.Type="button" Or oElement.Type="text" Then
      If oElement.className<>"icon" Then oElement.Style.FontFamily = SelectedFont1
      oElement.Style.FontSize = SelectedSize1 & "pt"
    End If
    If oElement.Type="checkbox" Or oElement.Type="radio" Then
      oElement.Style.Zoom = Scale
    End If
  Next

  BkP2.Style.FontFamily = "WingDings"
  BkP3.Style.FontFamily = "WingDings"
  BkP4.Style.FontFamily = "WingDings"

  Set oElements = Document.GetElementsByTagName("select")
  For Each oElement in oElements
    oElement.Style.FontFamily = SelectedFont1
    oElement.Style.FontSize = SelectedSize1 & "pt"
  Next

  UpdateRequiredWidth

  UpdateCheck

  Recenter

  Sec1.Style.MarginTop = fix1.ScrollHeight
  
  ' Refresh page
  Pg1.Style.Display = "none"
  Pg1.Style.Display = "inline"

End Sub

Sub RecenterButton
  WinSizePos = ""
  Recenter
End Sub

'Resize and position window
Sub Recenter
  If  WinSizePos="" Then
    Self.ResizeTo RequiredWidth, screen.availHeight
    Self.MoveTo (screen.availWidth - RequiredWidth) / 2, 4
  End If
End Sub

Sub UpdateFont2
  SelectedFont2 = Font2.Options(Font2.SelectedIndex).Value
  Txt1.Style.FontFamily = SelectedFont2
  Txt2.Style.FontFamily = SelectedFont2
  Pg3.Style.Display = "none"
  Pg3.Style.Display = "inline"
End Sub

Sub UpdateSize2
  SelectedSize2 = Size2.Options(Size2.SelectedIndex).Value
  Txt1.InnerText = ""
  Txt2.InnerText = ""
  Txt1.Value = Txt1Data
  Txt2.Value = Txt2Data
  Txt1.Style.FontSize = SelectedSize2 & "pt"
  Txt2.Style.FontSize = SelectedSize2 & "pt"
  Pg3.Style.Display = "none"
  Pg3.Style.Display = "inline"
End Sub

' Change width of display columns class based on 1-9 value selected in dropdown menu.
' This effectively goes from no horizontal scroll bar (1=100%) up to 900% to allow
' column heading list to wrap or be displayed on a single line, even if a large number
' of column headings are selected.

Sub xschange
  x = xs.SelectedIndex
  If x=1 Then v = "" Else v = Int(RequiredWidth * x)
  Set oElements = Document.GetElementsByTagName("div")
  For Each oElement in oElements
    If oElement.ClassName="cta" Then oElement.Style.Width = v
  Next
  xs.blur
End Sub

Sub ShowC1(id)
  Document.GetElementByID("V~" & id).innerHTML = id
End Sub

Sub ShowC2(id,ftname)
  Document.GetElementByID("V~" & id).innerHTML = Replace(ftname,"`","'")
End Sub

Sub PixelInput(id)
  If Not window.event.altKey Then Exit Sub
  a = Document.GetElementByID("W~" & id).Value
  b = ""
  If IsNumeric(a) Then b = Int(BaseScale * 8 * a)
  b = Modal(LRArrow & ".", ArrLang(28) & ";" & b, window.event.clientY - Int(Scale * 50))
  If Not IsNumeric(b) Then b = ""
  If b<>"" Then 
    b = Abs(Int(b))
    c = Int(b / BaseScale / 8)
    If c>999 Then c = 999
    If c<1 Then c = 1
    Document.GetElementByID("W~" & id).Value = c
  End If
End Sub

Sub SetInterface
  If IFace.SelectedIndex=1 Then
    lb.Style.visibility = "hidden"
    sb.Style.visibility = "hidden"
    ao.Style.visibility = "hidden"
    ica.Style.Display = "none"
    For i = 1 To UBFolderType
      Document.GetElementByID(i & "Inspect").Style.Display = "none"
    Next
  Else
    If Not wd.Checked Then
      lb.Style.visibility = "visible"
      sb.Style.visibility = "visible"
      ao.Style.visibility = "visible"
      ica.Style.Display = "inline"
      For i = 1 To UBFolderType
        If ArrIncludeFlag(i)="1" Then Document.GetElementByID(i & "Inspect").Style.Display = "inline"
      Next
    End If
  End If
  IFace.blur
End Sub

Sub SetTheme
  If UBound(ArrThemes)=-1 Then Exit Sub
  sTheme = "[" & ArrThemes(Theme.SelectedIndex-1) & "]"
  Document.body.Style.color = oThemes.Item(sTheme & "MainText")
  Document.body.Style.backgroundColor = oThemes.Item(sTheme & "MainBackground")
  fix1.Style.backgroundColor = oThemes.Item(sTheme & "MainBackground")
  fix2.Style.backgroundColor = oThemes.Item(sTheme & "MainBackground")
  txt1.Style.color = oThemes.Item(sTheme & "InspectText")
  txt1.Style.backgroundColor = oThemes.Item(sTheme & "InspectBackground")
  txt2.Style.color = oThemes.Item(sTheme & "InspectText")
  txt2.Style.backgroundColor = oThemes.Item(sTheme & "InspectBackground")
  SetColShowColor
  Set oElements = Document.GetElementsByTagName("select")
  For Each oElement in oElements
    oElement.Style.color = oThemes.Item(sTheme & "SelectText")
    oElement.Style.backgroundColor = oThemes.Item(sTheme & "SelectBackground")
    oElement.Style.borderColor = oThemes.Item(sTheme & "SelectBorder")
  Next
  Set oElements = Document.GetElementsByTagName("input")
  For Each oElement in oElements
    If oElement.Type="button" Then
      oElement.Style.color = oThemes.Item(sTheme & "ButtonText")
      oElement.Style.backgroundColor = oThemes.Item(sTheme & "ButtonBackground")
      oElement.Style.borderColor = oThemes.Item(sTheme & "ButtonBorder")
    End If
    If oElement.Type="text" Then
      oElement.Style.color = oThemes.Item(sTheme & "InputText")
      oElement.Style.borderColor = oThemes.Item(sTheme & "InputBorder")
      If oElement.Disabled = True Then
        oElement.Style.backgroundColor = oThemes.Item(sTheme & "InputDisabledBackground")
      Else
        oElement.Style.backgroundColor = oThemes.Item(sTheme & "InputBackground")
      End If
    End If
  Next
  Theme.blur
End Sub

Sub SetColShowColor
  Set oElements = Document.GetElementsByTagName("span")
  For Each oElement in oElements
    If oElement.ClassName="X1" Then oElement.Style.color = oThemes.Item(sTheme & "MainText")
    If oElement.ClassName="X2" Then oElement.Style.color = oThemes.Item(sTheme & "SearchPropText")
    If oElement.ClassName="X3" Then oElement.Style.color = oThemes.Item(sTheme & "MainText")
    If oElement.ClassName="X4" Then oElement.Style.color = oThemes.Item(sTheme & "SearchPropText")
  Next
End Sub

Sub Help
  x = "https://lesferch.github.io/WinSetView"
  If Beta Then x = "https://github.com/LesFerch/WinSetView/blob/beta/README.md"
  oWSH.Run x
End Sub

' HTML interface starts here with the script-generated pages

Sub BuildPages
  
  'Build page of Folder Types
  For i = 0 To UBFolderType
    Dim Xinc, Xcop, Xpas, Xln2, Xvue
    Xinc = "":Xcop = "": Xpas = "": Xln2 =""
    If i>0 Then
      Xinc = "<input type=checkbox id=" & i & "Include onclick=IncludeChange(" & i & ") Checked>"
      Xcop = "<div class=tip><input type=button class=icon id=" & i & "Copy value=📋 onclick=FTCopy(" & i & ")><span class=tiptext># 40 #</span></div> "
      Xpas = "<div class=tip><input type=button class=icon id=" & i & "Paste value=🖌️ onclick=FTPaste(" & i & ")><span class=tiptext># 41 #</span></div>"
      Xln2 = "<div class=fo><input type=checkbox id=" & i & "Inherit onclick=InheritChange(" & i & ") Checked># 21 # <input type=button class=icon id=" & i & "Inspect value=🔍 style='padding:0' onclick=Inspection(" & i & ")></div>"
      Xvue = "<div class=fo id=" & i & "FTView></div>"
    End If
    ArrHTML1(i) = Xinc & "<div class=ft id=" & i & "FTtitle>" & ArrFolderTypeLang(i) & "</div><br>" & Z &_
    "<div id=" & i & "FTsettings>" & Xln2 & Z &_
    "<div class=fo id=" & i & "FTcontrols><select id=" & i & "View onchange=ViewChange(" & i & ")></select>" & Z &_
    "<input type=text class=is id=" & i & "IconSize OnChange=IconSizeChange(" & i & ")>" & Z &_
    "<input type=button id=" & i & "SelCol value='# 24 #' onclick=SelectColumns(" & i & ")>" & Z &_
    "<div class=tip><input type=button class=icon id=" & i & "Option value=⚙ onclick=FTOptions(" & i & ")><span class=tiptext># 3 #</span></div>" & Z &_
    Xcop & Xpas & "</div>" & Z &_
    Xvue & Z &_
    "<div class=cta id=" & i & "ColShow></div>" & Z &_
    "<div class=fo># 22 #: <span id=" & i & "GroupBy></span></div>" & Z &_
    "<div class=fo># 23 #: <span id=" & i & "SortBy></span></div>" & Z &_
    "<div class='fo noshow' id=" & i & "FTDialogView># 44 #: <span id=" & i & "DialogView></span></div>" & Z &_
    "</div><br><br>" & Z
  Next

  'Build column heading selection page
  For i = 0 To UBound(ArrColData)
    If ArrColData(i)="" Then ReDim Preserve ArrColData(UBound(ArrColData)-1): Exit For
    ArrColumnEntry = Split(ArrColData(i),";")
    C1 = ArrColumnEntry(1): C2 = ArrColumnEntry(0)
    ColShowColor(C1)
    ArrHTML2(i) = "<tr>" &_
    "<td><input type=radio name=GroupBy onclick=RBGClick('" & C1 & "') id=G~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=radio name=RBS1 onclick=RBS1Click('" & C1 & "') id=S1~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=radio name=RBS2 onclick=RBS2Click('" & C1 & "') id=S2~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=radio name=RBS3 onclick=RBS3Click('" & C1 & "') id=S3~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=radio name=RBS4 onclick=RBS4Click('" & C1 & "') id=S4~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=text class=cw id=W~" & C1 & " maxlength=3 onclick=PixelInput('" & C1 & "')></td>" &_
    "<td><input type=checkbox onclick=CBMClick('" & C1 & "') id=M~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><input type=checkbox onclick=CBPClick('" & C1 & "') id=P~" & C1 & " Value='" & C1 & "'></td>" &_
    "<td><span id=V~" & C1 & " class=" & x & " onmouseover='ShowC1 " & Q & C1 & Q &_
    "' onmouseout='ShowC2 " & Q & C1 & Q & "," & Q & Replace(C2,"'","`") & Q & "'>" & C2 & "</span></td>" &_
    "<td><input type=text style=display:none id=L~" & C1 & " Value='" & C2 & "'></td>" &_
    "<td><input type=text style=display:none id=N~" & C1 & "></td></tr>"
  Next

  BodyHTML = TaggedHTML
  BodyHTML = Replace(BodyHTML,"+++",Join(ArrHTML1))
  BodyHTML = Replace(BodyHTML,"~~~",Join(ArrHTML2))
  
  '0 not used, 1 is title, 2 is first label
  For i = 2 To UBound(ArrLang)
    Tag = "# " & i & " #"
    Item = ArrLang(i)
    If Item<>"" Then
      BodyHTML = Replace(BodyHTML,Tag,Item)
    End If
  Next

  If WinVer="7" Or WinVer="8" Then BodyHTML = Replace(BodyHTML,"⮬","↑")

  Document.Body.innerHTML = BodyHTML

End Sub

Sub document_onKeyDown
  'Recenter window if F1-F12 is pressed (also prevents F5 or Ctrl-F5 refresh)
  If window.event.keyCode>=112 And window.event.keyCode<=123 Then
    window.event.keyCode = 0
    WinSizePos = ""
    Recenter
  End If
End Sub

</script>

<style>
  @font-face {font-family:Segoe UI Emoji; src:url(AppParts/seguiemj.eot)}
  body  {background-color:LemonChiffon; font-family:Segoe UI; font-size:11pt}
  input[type="button"] {font-family:Segoe UI Emoji; font-size:11pt}
  .icon {font-family:Segoe UI Emoji}
  select {font-family:Segoe UI; font-size:11pt}
  textarea {font-family:Consolas; font-size:10pt}
  h2    {font-size:1.5em}
  h3    {font-size:1.2em}
  #gn   {margin-top:3pt}
  #ka   {margin-top:3pt}
  #nf   {margin-top:3pt}
  #ua   {margin-top:3pt}
  #cm   {margin-top:3pt}
  #si   {margin-top:3pt}
  #sh   {margin-top:3pt}
  #so   {margin-top:3pt}
  .cta  {margin-top:5pt; width:100%}
  #fix1 {position:fixed; left:0; top:0; width:100%; background-color:LemonChiffon}
  #f1in {margin-left:5pt; margin-top:5pt}
  #twid {display:inline; white-space:nowrap}
  #bwid {display:inline; white-space:nowrap}
  #Sec1 {margin-top:106pt}
  #fix2 {position:fixed; left:0; top:0; width:100%; background-color:LemonChiffon}
  #f2in {margin-top:5pt; margin-bottom:0pt}
  #Pg2  {display:none}
  #Pg3  {display:none}
  #Pg4  {display:none}
  .cw   {width:38%; height:100%; font-size:1em}
  #BkP2 {float:right; margin-right:10pt; font-family:WingDings; width:3em}
  #BkP3 {float:right; font-family:WingDings; width:3em}
  #BkP4 {float:right; font-family:WingDings; width:3em}
  .fl1  {float:left; margin-left:5pt}
  .fl2  {float:left; margin-left:3pt}
  .fl3  {float:left; margin-left:3pt; margin-right:3pt}
  .ft   {font-size:1.2em; font-weight:bold; margin-top:1pt; margin-bottom:1pt; display:inline}
  .ftl  {font-size:1.2em; font-weight:light; margin-top:1pt; margin-bottom:1pt; display:inline}
  .ft2  {font-weight:bold; margin-top:1pt; margin-bottom:1pt; margin-left:5pt}
  .fo   {margin-top:5pt}
  #tab1 {margin-bottom:5pt; margin-top:9pt; margin-left:11px; border-collapse:collapse}
  #tcin {margin-top:62pt}
  #tab2 {border-collapse:collapse}
  #cs   {width:98%; margin-left:5pt; margin-top:8pt; white-space:nowrap; overflow-x:scroll}
  #Txt1 {width:100%; margin-top:5pt; overflow:auto}
  #Txt2 {width:100%; margin-top:5pt; overflow:auto}
  .X1   {color:black}
  .X2   {color:blue}
  .X3   {color:black}
  .X4   {color:blue}
  .X1HC {font-style:normal}
  .X2HC {font-style:italic}
  .X3HC {font-style:normal}
  .X4HC {font-style:italic}
  #tpv  {margin-left:1.5em; margin-top:3pt}
  .is   {width:2em}
  .noshow {display:none}

  #tab1 tbody td {text-align:center}
  #tab2 tbody td {text-align:center}
  #tab2 tbody td+td+td+td+td+td+td+td+td {text-align:left}

  .tip {
    display:inline;
    font-weight:normal;
    color:Black
  }
  .tiptext {
    visibility:hidden;
    position:absolute;
    z-index:1;
    white-space:nowrap;
    background-color:Azure;
    text-align:center;
    padding:0.3em 0.3em 0.3em 0.3em;
    border:1px solid black;
    margin-top:2.2em;
    margin-left:-1.5em
  }
  .tip:hover .tiptext {
    visibility:visible
  }

</style>
</head>
<body>
  <div id=Pg1>
    <div id=fix1>
    <div id=f1in>
      <div id=twid>
        <div class=tip><input type=button id=hb value='# 31 #' onclick=Help()>
        <span id=Vp1 class=tiptext></span></div>
        <div class=tip><input id=bc1 type=button value='⛶' onclick=RecenterButton()>
        <span class=tiptext># 42 #</span></div>
        <select id=Lang onchange='UpdateLang(1)'></select>
        <select id=IFace onchange='SetInterface()'>
          <option selected disabled># 32 #</option>
          <option># 33 #</option>
          <option># 34 #</option>
        </select>
        <select id=Font1 onchange=UpdateFont1()></select>
        <select id=Size1 onchange=UpdateFont1()></select>
        <select id=xs onchange=xschange()></select>
        <select id=Theme onchange=SetTheme()>
        </select>
      </div>
      <br><br>
      <input type=checkbox id=wd onclick=OptionReset()># 7 #<br><br>
      <div id=bwid>
        <input type=button id=xb value='# 2 #' onclick=Submit()>
        <input type=button id=ob value='# 3 #' onclick=Options()>
        <input type=button id=rb value='# 6 #' onclick=Restore()>
        <input type=button id=lb value='# 4 #' onclick=LoadSettings()>
        <input type=button id=sb value='# 5 #' onclick=SaveSettings()>
        <br><br>
      </div>
    </div>
    </div>
    <div id=Sec1>
    <input type=checkbox id=ica onclick=IncludeAll() Checked>
    +++
    </div>
  </div>

  <div id=Pg2>
    <div id=fix2>
    <div id=f2in>
      <input type=button value='&#223;' id=BkP2 onclick=P2Exit()>
      <div class='tip fl1'><input type=button value='# 31 #' onclick=Help()>
      <span id=Vp2 class=tiptext></span></div>
      <div class='tip fl2'><input id=bc2 type=button value='⛶' onclick=RecenterButton()>
      <span class=tiptext># 42 #</span></div>
      <div class='tip fl3'><input id=ba type=button value='⮀' onclick=Arrange()>
      <span class=tiptext># 36 #</span></div>
      <h3 id=P2tt class=ft2></h3>
      <div id=cs></div>
      <table id=tab1>
        <colgroup>
          <col style='width:3.2em; min-width:3.2em; max-width:3.2em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:4.6em; min-width:4.6em; max-width:4.6em'>
          <col style='width:3.0em; min-width:3.0em; max-width:3.0em'>
          <col style='width:3.0em; min-width:3.0em; max-width:3.0em'>
        </colgroup>
        <tbody>
          <tr>
          <th><div class=tip><input type=button id=cbg value='{⮬}' onclick=GroupByClick() style='height:2em; padding:0; width:2em'><span class=tiptext># 22 #</span></div></th>
          <th><div class=tip><input type=button id=cbs1 value='⮬' onclick=SortBy1Click() style='height:2em; padding:0; width:1.6em'><span class=tiptext># 14 #</span></div></th>
          <th><div class=tip><input type=button id=cbs2 value='⮬' onclick=SortBy2Click() style='height:2em; padding:0; width:1.6em'><span class=tiptext># 15 #</span></div></th>
          <th><div class=tip><input type=button id=cbs3 value='⮬' onclick=SortBy3Click() style='height:2em; padding:0; width:1.6em'><span class=tiptext># 16 #</span></div></th>
          <th><div class=tip><input type=button id=cbs4 value='⮬' onclick=SortBy4Click() style='height:2em; padding:0; width:1.6em'><span class=tiptext># 17 #</span></div></th>
          <th><div class=tip><input type=button id=cbw value='⟷' onclick=WidthClick() style='height:2em; padding:0; width:2em'><span class=tiptext># 18 #</span></div></th>
          <th><div class=tip><input type=button id=cbo value='⋮⋮⋮' onclick=View1Click() style='height:2em; padding:0; width:2em'><span class=tiptext># 19 #</span></div></th>
          <th><div class=tip><input type=button id=cbc value='⋮' onclick=View2Click() style='height:2em; padding:0; width:2em'><span class=tiptext># 20 #</span></div></th>
          </tr>
        </tbody>
      </table>
    </div>
    </div>
    <div id=tc>
    <div id=tcin>
      <table id=tab2>
        <colgroup>
          <col style='width:3.2em; min-width:3.2em; max-width:3.2em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:1.9em; min-width:1.9em; max-width:1.9em'>
          <col style='width:4.6em; min-width:4.6em; max-width:4.6em'>
          <col style='width:3.0em; min-width:3.0em; max-width:3.0em'>
          <col style='width:3.0em; min-width:3.0em; max-width:3.0em'>
        </colgroup>
        <tbody>
          ~~~
        </tbody>
      </table>
    </div>
    </div>
  </div>

  <div id=Pg3>
    <div id=P3hd>
      <input type=button value='&#223;' ID=BkP3 OnClick=Back2Pg1()>
      <select id=Font2 onchange=UpdateFont2()></select>
      <select id=Size2 onchange=UpdateSize2()></select>
    </div>
    <textarea id=Txt1 wrap=off onscroll=Txt1Scroll()></textarea>
    <textarea id=Txt2 wrap=off onscroll=Txt2Scroll()></textarea>
  </div>

  <div id=Pg4>
    <input type=button value='&#223;' ID=BkP4 OnClick=Back2Pg1()>
    <div class='tip fl2'><input type=button value='# 31 #' onclick=Help()>
    <span id=Vp3 class=tiptext></span></div>
    <div class='tip fl3'><input id=bc3 type=button value='⛶' onclick=RecenterButton()>
    <span class=tiptext># 42 #</span></div>
    <h2># 3 #</h2>
    <input type=checkbox id=se Checked># 8 #<br><br>
    <input type=checkbox id=cv Checked># 27 #<br><br>
    <input type=checkbox id=cm># 47 #<br><br>
    <input type=checkbox id=si># 48 #<br><br>
    <input type=checkbox id=sh># 49 #<br><br>
    <input type=checkbox id=nf># 35 #<br><br>
    <input type=checkbox id=ua># 50 #<br><br>
    <div id=Sec4>
      <input type=checkbox id=so Checked onclick=OptionSearchOnly()># 9 #<br><br>
      <input type=checkbox id=vf Checked># 29 #<br><br>
      <input type=checkbox id=tp onclick=OptionThisPC()># 13 #<br>
      <div id=otp><select id=tpv></select> <input type=checkbox id=tpg checked># 25 #</div><br>
      <div id=ao>
      <input type=checkbox id=gn Checked># 10 #<br><br>
      <input type=checkbox id=ka># 11 #<br><br>
      </div>
    </div>
  </div>
</body>
</html>